## 1.创建ods_stu表(基表)
```
create table ods_stu
(
id          int,
sname       string,
tel         string,
create_time string
)partitioned by(dt string)
row format delimited fields terminated by '\t';
```
## 2.linux的 /lx/ods_stu.txt 插入以下数据
1	张三	13600000001	2022-06-12
2	李四	13700000001	2022-06-12
3	王五	13800000001	2022-06-12

## 3.把linux中的数据导入到ods_stu表中
`load data local inpath '/lx/ods_stu.txt' into table ods_stu partition(dt='2022-06-12');`

## 4.创建拉链表 dw_stu_his
```
create table dw_stu_his
(
id          int,
sname       string,
tel         string,
create_time string,
start_time  string,
end_time    string
)partitioned by(dt string)
row format delimited fields terminated by '\t';
```
## 5.把ods_stu表的数据导入到拉链表dw_stu_his中(数据初始化)

```
insert overwrite table dw_stu_his partition(dt='9999-12-31')
select id, 
	   sname,
	   tel,
	   create_time,
	   create_time as start_time,
	   '9999-12-31' as end_time 
from ods_stu;
```
## 6.更新ods数据(基表)
```
insert overwrite table ods_stu partition(dt='2022-06-15')
select 1,'张三', '13600000009' , '2022-06-15'
union all
select 4,'老六', '13900000888' , '2022-06-15';
```
## 7.更新拉链表数据
-----新用户开链：把老六的数据放入9999-12-31分区中
-----老用户开链：把张三的最新的数据也放入9999-12-31分区中
-----老用户闭链：把张三以前的数据放入到2022-06-15分区中

insert overwrite table  dw_stu_his
--①：选出新用户开链和老用户开链的数据
select 
 nvl(t1.id,          t2.id), 
 nvl(t1.sname,       t2.sname),
 nvl(t1.tel,         t2.tel),
 nvl(t1.create_time, t2.create_time),
 nvl(t1.create_time, t2.start_time) as start_time,
 '9999-12-31'   as end_time,
 '9999-12-31'   as dt
from
 (select * from ods_stu    where dt = '2022-06-15') t1
full join
 (select * from dw_stu_his where dt = '9999-12-31') t2
on t1.id = t2.id

union all

--②：选出老用户闭链的数据
select 
 t2.id,
 t2.sname,
 t2.tel,
 t2.create_time,
 t2.start_time,
 t1.create_time  as end_time,
 t1.create_time  as dt
from
 (select * from ods_stu    where dt = '2022-06-15') t1
join
 (select * from dw_stu_his where dt = '9999-12-31') t2
on t1.id = t2.id;